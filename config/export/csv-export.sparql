PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX regorg: <https://www.w3.org/ns/regorg#>

SELECT ?s AS ?mandataris ?start ?eind ?rangorde ?status ?statusLabel ?persoon ?voornaam ?achternaam ?roepnaam ?mandaat ?bestuursfunctie ?bestuursfunctieLabel ?bestuursorgaan ?bestuursorgaanLabel ?bestuursorgaanClassificatie ?bestuursorgaanClassificatieLabel ?bestuurseenheidLabel ?bestuurseenheidClassificatie ?bestuurseenheidClassificatieLabel ?werkingsgebied ?werkingsgebiedLabel ?werkingsgebiedNiveau ?fractie ?fractieNaam ?beleidsdomein ?beleidsdomeinLabel FROM <http://mu.semte.ch/application> WHERE {
  ?s a mandaat:Mandataris .

  OPTIONAL {
    ?s mandaat:start ?unformattedStart .
    BIND(IF(STRLEN(STR(?unformattedStart)) > 0,
      CONCAT(
        STR(YEAR(?unformattedStart)), "/",
        IF(MONTH(?unformattedStart) < 10, "0", ""), STR(MONTH(?unformattedStart)), "/",
        IF(DAY(?unformattedStart) < 10, "0", ""), STR(DAY(?unformattedStart)), " ",
        IF(HOURS(?unformattedStart) < 10, "0", ""), STR(HOURS(?unformattedStart)), ":",
        IF(MINUTES(?unformattedStart) < 10, "0", ""), STR(MINUTES(?unformattedStart)), ":",
        IF(SECONDS(?unformattedStart) < 10, "0", ""), STR(SECONDS(?unformattedStart)
      )), "") as ?start) .
  }

  OPTIONAL {
    ?s mandaat:einde ?unformattedEind .
    BIND(IF(STRLEN(STR(?unformattedEind)) > 0,
      CONCAT(
        STR(YEAR(?unformattedEind)), "/",
        IF(MONTH(?unformattedEind) < 10, "0", ""), STR(MONTH(?unformattedEind)), "/",
        IF(DAY(?unformattedEind) < 10, "0", ""), STR(DAY(?unformattedEind)), " ",
        IF(HOURS(?unformattedEind) < 10, "0", ""), STR(HOURS(?unformattedEind)), ":",
        IF(MINUTES(?unformattedEind) < 10, "0", ""), STR(MINUTES(?unformattedEind)), ":",
        IF(SECONDS(?unformattedEind) < 10, "0", ""), STR(SECONDS(?unformattedEind)
      )), "") as ?eind) .
  }

  OPTIONAL { ?s mandaat:rangorde ?rangorde . }
  OPTIONAL {
    ?s mandaat:status ?status .
    ?status skos:prefLabel ?statusLabel .
  }

  OPTIONAL {
    ?s mandaat:isBestuurlijkeAliasVan ?persoon .
    OPTIONAL { ?persoon foaf:familyName ?achternaam . }
    OPTIONAL { ?persoon persoon:gebruikteVoornaam ?voornaam . }
    OPTIONAL { ?persoon foaf:name ?roepnaam . }
  }

  OPTIONAL {
    ?s org:holds ?mandaat .
    OPTIONAL {
      ?mandaat org:role ?bestuursfunctie .
      OPTIONAL { ?bestuursfunctie skos:prefLabel ?bestuursfunctieLabel . }
    }

    OPTIONAL {
      ?mandaat ^org:hasPost ?bestuursorgaanInTijd .
      ?bestuursorgaanInTijd mandaat:isTijdspecialisatieVan ?bestuursorgaan .
      OPTIONAL { ?bestuursorgaan skos:prefLabel ?bestuursorgaanLabel . }
      OPTIONAL {
        ?bestuursorgaan besluit:classificatie ?bestuursorgaanClassificatie .
        OPTIONAL { ?bestuursorgaanClassificatie skos:prefLabel ?bestuursorgaanClassificatieLabel }
      }

      OPTIONAL {
        ?bestuursorgaan besluit:bestuurt ?bestuurseenheid .
        OPTIONAL { ?bestuurseenheid skos:prefLabel ?bestuurseenheidLabel . }
        OPTIONAL {
          ?bestuurseenheid besluit:classificatie ?bestuurseenheidClassificatie .
          OPTIONAL { ?bestuurseenheidClassificatie skos:prefLabel ?bestuurseenheidClassificatieLabel . }
        }
        OPTIONAL {
          ?bestuurseenheid besluit:werkingsgebied ?werkingsgebied .
          OPTIONAL { ?werkingsgebied rdfs:label ?werkingsgebiedLabel . }
          OPTIONAL { ?werkingsgebied ext:werkingsgebiedNiveau ?werkingsgebiedNiveau . }
        }
      }
    }
  }

  OPTIONAL {
    ?s org:hasMembership/org:organisation ?fractie .
    OPTIONAL { ?fractie regorg:legalName ?fractieNaam . }
  }

  OPTIONAL {
    ?s mandaat:beleidsdomein ?beleidsdomein .
    OPTIONAL { ?beleidsdomein skos:prefLabel ?beleidsdomeinLabel . }
  }

}
